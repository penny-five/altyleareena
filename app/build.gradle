/*
 * Copyright 2015 Joonas Lehtonen
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.4'
        classpath 'me.tatarka:gradle-retrolambda:3.2.0'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'me.tatarka.retrolambda'

def versionMajor = 0
def versionMinor = 0
def versionPatch = 1
def versionBuild = 0

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        applicationId "com.github.pennyfive.altyleareena"
        minSdkVersion 15
        targetSdkVersion 22
        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        buildConfigField("String", "YLE_API_ENDPOINT", "\"https://external.api.yle.fi/v1/programs/\"")
        buildConfigField("String", "YLE_CLOUDINARY_ENDPOINT", "\"http://images.cdn.yle.fi/\"");
        /* Yle API credentials must be placed in gradle.properties */
        buildConfigField("String", "YLE_APP_ID", getProperty("yle_app_id"))
        buildConfigField("String", "YLE_APP_KEY", getProperty("yle_app_key"))
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix '.debug'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    // Android support libraries
    compile 'com.android.support:support-v4:22.2.1'
    compile 'com.android.support:appcompat-v7:22.2.1'
    compile 'com.android.support:design:22.2.1'
    compile 'com.android.support:cardview-v7:22.2.1'
    compile 'com.android.support:recyclerview-v7:22.2.1'
    compile 'com.android.support:support-annotations:22.2.1'

    // REST
    compile 'com.squareup.retrofit:retrofit:1.9.0'

    // Dependency injection
    compile 'com.google.dagger:dagger:2.0.1'
    apt 'com.google.dagger:dagger-compiler:2.0.1'

    // View binding
    compile 'com.jakewharton:butterknife:7.0.1'

    // Image loading & caching
    compile 'com.github.bumptech.glide:glide:3.6.1'

    // Reactive programming
    compile 'io.reactivex:rxjava:1.0.12'
    compile 'io.reactivex:rxandroid:0.25.0'

    // Required by Dagger & RxJava
    compile 'javax.inject:javax.inject:1'
    compile 'javax.annotation:jsr250-api:1.0'

    // Roboto
    compile 'com.github.johnkil.android-robototextview:robototextview:2.3.0'

    // Material style progress spinner
    compile 'com.pnikosis:materialish-progress:1.5'

    // Parcelables
    compile 'me.tatarka:auto-parcel-gson:0.1'
    apt 'me.tatarka:auto-parcel-gson-processor:0.1'
}
